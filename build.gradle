plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = 'GeckoLib'
		url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
		content {
			includeGroupByRegex("software\\.bernie.*")
			includeGroup("com.eliotlash.mclib")
		}
	}
	maven { url 'https://maven.blamejared.com' }
	maven { url "https://cursemaven.com" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
	maven { url "https://maven.architectury.dev/" }
	maven { url 'https://api.modrinth.com/maven' }
	maven {
		name "KosmX's maven"
		url 'https://maven.kosmx.dev/'
	}
	mavenCentral()
}

loom {
	runs {
		datagen {
			inherit server
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=too-many-spells"

			runDir "build/datagen"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation("maven.modrinth:spell-engine:${project.spell_engine_version}+1.21.1-fabric")
	modImplementation("maven.modrinth:spell-power:${project.spell_power_version}+1.21.1-fabric")
	modImplementation "maven.modrinth:ranged-weapon-api:${project.ranged_weapon_api_version}"
	modImplementation files("lib/summoner-lib-1.0.0.jar")
	modImplementation "curse.maven:critical-strike-1379562:7203013"
	// Marium's Soulslike Weaponry
	modCompileOnly "curse.maven:mariums-soulslike-weaponry-632066:6948448"
	modCompileOnly("software.bernie.geckolib:geckolib-fabric-${geckolib_version}")
	modCompileOnly "curse.maven:attributefix-${project.attributefix}"
	modRuntimeOnly("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config}")
	modRuntimeOnly("dev.kosmx.player-anim:player-animation-lib-fabric:${project.player_anim}")
	modCompileOnly "curse.maven:bookshelf-${project.bookshelf}"
	modCompileOnly "curse.maven:prickle-${project.prickle}"

	modImplementation("maven.modrinth:tiny-config:${project.tiny_config_version}-fabric")

}

sourceSets {
	main {
		resources {
			srcDirs += [
					'src/main/generated'
			]
		}
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}